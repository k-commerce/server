buildscript {
    ext {
        jwtVersion = "0.11.5"
        mapstructVersion = "1.5.3.Final"
        springdocVersion = "1.6.14"
        querydslVersion = "5.0.0"
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "2.7.9"
    id "io.spring.dependency-management" version "1.0.15.RELEASE"
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = "com.kcommerce"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    // spring-boot
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // spring-security
    implementation "org.springframework.boot:spring-boot-starter-security"
    testImplementation "org.springframework.security:spring-security-test"

    // lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // mysql
    runtimeOnly "com.mysql:mysql-connector-j"

    // jwt
    implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

    // mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // springdoc
    implementation "org.springdoc:springdoc-openapi-webmvc-core:${springdocVersion}"

    // querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}"

    // h2
    testImplementation "com.h2database:h2"
}

tasks.named("test") {
    useJUnitPlatform()
}

// querydsl
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
